# Gradle

## Gradle이란?

- Gradle은 goovy를 기반(코틀린도 사용가능)으로 한 **빌드도구**이며 **Ant와 Maven**의 단점을 보완하고 장점을 취합하여 만든 오픈소스로 공개된 빌드 도구
- XML 언어가 아닌 자바 문법과 유사한 Groovy를 사용, 최근엔 kotlin도 사용하는 추세
- Gradle Wrapper를 사용해서 설치없이 사용이 가능하다.
- DSL(Domain Specific Language) 활용하여 깔끔한 빌드 스크립트를 구현할 수 있다. ([https://docs.gradle.org/current/dsl/](https://docs.gradle.org/current/dsl/))

## Gradle vs Maven

- [https://gradle.org/maven-vs-gradle/](https://gradle.org/maven-vs-gradle/)

### Maven 단점

- Gradle에 비해 상대적으로 러닝커브가 높다. (개인차있음)
- 프로젝트의 규모가 커지면 xml코드가 복잡해져서 라이브러리 관리가 어렵고 가독성이 떨어진다.
    - 멀티 프로젝트 일때도 여러 프로젝트의 pom.xml을 따로 관리해야하기 때문에 번거롭다.

### Gradle 장점

- Build Cache, Gradle Daemon을 사용해서 메이븐보다 성능이 월등히 우수하다. (공홈 참고)
- **Gradle Wrapper 제공**을 통해 작업자가 로컬에 빌드도구를 직접 설치할 필요가 없다.
- 최상위 프로젝트에서 하위프로젝트를 한번에 관리할 수 있다. (**손쉬운 멀티 프로젝트 기능 제공**)
    - 하위프로젝트의 build script를 일일히 열어서 볼 필요가 없다. (프로젝트 규모가 커질수록 장점)
    - Cross project Configuration
- 프로그래밍 방식으로 스크립트를 작성하기 때문에 동적 스크립트 작성이 쉽다.
    
    

## Gradle Wrapper?

- 예를들어 Gradle로 구성된 어떤 프로젝트를 git으로 받아서 빌드하려면 Gradle이 해당 시스템에 설치 되어 있어야 한다. 하지만 설치하려는 Gradle의 버전과 내장된 Gradle의 버전이 다른 경우 프로젝트가 동작하지 않는 경우가 생긴다. Gradle은 이러한 문제를 해결하기 위해 Gradle Wrapper를 제공한다.
- Gradle Wrapper는 gradlew 또는 gradlew.bat으로 실행하며 내장 Gradle을 통해서 동작한다.

```bash
// 일반 gradle 사용
gradle task

// gradlew 사용
./gradlew task

// gradle wrapper 생성
gradle wrapper

// 특정버전의 gradle wrapper 생성
gradle wrapper --gradle-version 6.5
```

- 위 명령어를 실행하면 다음과같은 wrapper 파일이 생성된다.
    
    ![스크린샷 2022-02-25 오후 3.21.54.png](Gradle%20032893aaf159450f856fd843fcb77420/a.png)
    

## Gradle 설치 (v7)

- gradle 버전에맞는 jdk를 확인 후 선행으로 설치해야 한다.

[https://gradle.org/install/](https://gradle.org/install/)

- mac에서 설치

```bash
// mac
brew install gradle
```

- window

[https://gradle.org/releases/](https://gradle.org/releases/)

### 환경변수 설정

- 각각 운영체제에 맞게 설정

## Gradle 프로젝트 폴더 구조

- Gradle 프로젝트를 생성하거나 gradle init —type java-application 명령어를 실행하면 나오는 폴더구조

```
├──**build.gradle**
├──gradle
│   └──wrapper
│       ├──gradle-wrapper.jar
│       └──gradle-wrapper.properties
├──**gradlew**
├──**gradlew.bat**
├──**settings.gradle**
└──src
    ├──main
    │   └──java
    │       └──App.java
    └──test
        └──java
            └──AppTest.java
```

- src폴더구조는 maven과 동일하다.

## Gradle의 스크립트, 속성파일

- 초기화 스크립트 (init.gradle)
    - 사용자 정보, 실행환경, 초기 설정값 등을 정의
- **설정 스크립트 (settings.gradle)**
    - **프로젝트 명, 프로젝트 구조 등을 정의**
- **빌드 스크립트 (build.gradle)**
    - **빌드 수행과 관련된 의존성, task 등을 정의**
- 속성파일 (gradle.properties)
    - 빌드 환경 속성등을 정의

## Gradle 기본 사용법

- 테스트 폴더에 build.gradle 파일을 생성한 후 task 코드 작성

```groovy
// build.gralde
task gradleTest {
	println "Gradle Test!!"
}

// 실행
gradle gradleTest
```

## Groovy

- [http://groovy-lang.org](http://groovy-lang.org/)

```groovy
// 자바와 같이 형 지정
String id = 'gradle'

// 형 지정 생략 (Groovy 형 지정 특징)
def id == 'gradle'
println id.toUpperCase()

// 문자열 안에 변수 사용
String id = "ID: ${id}" // $id도 가능

// 리스트
def list = ['groovy', 'java']
println id[1]

// 맵
def map = ['a': 'groovy', 'b': 'java']
println id['a']
```

## 프로젝트 속성 지정방법

### properties 파일 사용해서 속성 추가

```groovy
// gradle.properties
msg=Gradle Properties Test

// gradle.build
task hello {
	println msg
}
```

### 명령어 인수로 속성 추가

```groovy
task hello {
	println msg
}

// shell로 실행
gradle **-Pmsg**='Gradle Properties Test' hello
```

## Gradle & Groovy 실습

### Task 실습

- 반복문

```groovy
task taskExample {
    10.times { println "$it" }
}

3.times { counter ->
    task "taskExample$counter" {
        println "task counter: $counter"
    }
}

taskExample1.dependsOn taskExample0, taskExample2

// 실행
gradle taskExample1
0
1
2
```

- doFirst, doLast

```groovy
task task1 {
    doLast {
        println ">>> doLast!"
    }
    doFirst {
        println ">>> doFirst!"
    }
}

// 실행
gradle -q task1
>>> doFirst!
>>> doLast!
```

- 조건에 따른 빌드

```groovy
// 1
task exeTask {
    onlyIf {
        buildType == 'partial-build'
    }
    println "Gradle Build Success"
}

// 이것도 가능
exeTask.onlyIf {
    buildType == 'partial-build'
}

// 실행
gradle **-PbuildType**='partial-buid' exeTask

// 2
task exeTask {
    println "Build Success"

    if (process == 'error') {
        println "error"
    }

    println "Build End"
}

// 실행
gradle -Pprocess=error exeTask
Build Success
error
Build End
```

## build.gradle 작성하기

- Gradle의 스크립트 파일은 기본적으로 **처리문과 스크립트 블록**으로 구성된다.
- 처리문 (지역변수, 속성 설정 및 메소드 실행 등 일반적인 언어의 처리문과 같음)

```groovy
def message = 'Gradle Build'

task hello {
    println "message = ${message}"
}
```

- 스크립트 블록 (특정 설정을 위한 영역)

```groovy
buildscript {
	...
}
configurations {
	...
}
repositories {
	...
}
dependencies {
	...
}
subprojects {
	...
}
allprojects {
	...
}
```

### 주요 스크립트 블록

([https://docs.gradle.org/current/dsl/](https://docs.gradle.org/current/dsl/)) 참고

- buildscript (gradle 자체 설정)
- plugins (plugin 설정)
- repositories (중앙저장소 설정)
- dependencies (의존 관계 설정)
    - implementation
    - classpath
- configurations (환경 구성)
- subprojects (서브 프로젝트 설정)
- allprojects (전체 프로젝트 설정)

### buildscript, allprojects 차이

- buildscript 설정 섹션은 gradle 자체를 위한 섹션이다. (gradle이 빌드를 수행하는 방법에 대한 설정)
- allprojects는 Gradle에서 빌드하는 모듈을 위한 섹션이다.
    - 각 모듈에 대한 종속성은 고유하며 각각 모듈 내의 build.gradle 파일에 존재하지만 모든 모듈에서 공통으로 공유하는 종속성은 allprojects에 설정할 수 있다.
    

### buildscript

- 빌드를 위해 실행되는 스크립트 (gradle 스크립트 자체 설정임)
- 빌드 시 외부 라이브러리를 가져와야할 때 사용하며, repositories와 dependencies로 구성된다.
- 심플한 프로젝트에서는 필요없음

```groovy
buildscript {
	// 변수 설정
	ext {
  	springBootVersion = '1.5.9.RELEASE'
  }
	// buildscript dependencies에서 의존하는 라이브러리 저장소 설정
  repositories {
  	mavenCentral()
  }
	// 
  dependencies {
      classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
      classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"
  }
}
```

### plugins

- Gradle 모델 확장, 규칙에따라 프로젝트를 구성

```groovy
plugins {
	id 'java'
	id 'war'
}

or 

apply plugin: 'war'
apply plugin: 'java'
```

### repositories

- 라이브러리를 가져오는 공개저장소 설정
- **Maven 중앙저장소 : mavenCentral()**
- Bintray 저장소 : jcenter() - maven 단점 보완으로 많이 쓰는 추세...였는데 2월11일부로 지원종료
- Google 저장소 :  google() - android

### dependencies

- repositories에서 지정한 저장소에서 가져올 라이브러리를 등록

<aside>
💡 config ‘그룹명’:’모듈명’:’version’
config group: ‘그룹명', name: ‘모듈명', version: ‘버전’

</aside>

- 라이브러리 의존관계 정의

```groovy
plugins {
	id 'java'
}

repositories {
	...
}

dependencies {
	implementation "org.springframework.boot:spring-boot-gradle-plugin:2.6.3"
	testImplementation group:'org.springframework.boot', name:'spring-boot-gradle-plugin', version: '2.
	compileOnly ... 
	testCompileOnly... 
	runtimeOnly ... 
	testRuntimeOnly ...
}
```

- 라이브러리 추가하는 설정 (maven의 scope)
    - implementation을 사용하면 필요한 모듈만 가져오기때문에 빌드속도가 빠르다. (일반적으로 사용)
    - testCompile, testRuntime, testImplementation은 빌드과정에서 배포본에 포함되지 않는다.
    - implementation은 컴파일, 런타임 두가지 모두에 사용된다.
    - **해당 라이브러리가 어떤 단계에서 필요한지 명확히 구분 후 사용해야 리소스 낭비를 줄일 수 있다.**
    
    ![스크린샷 2022-02-27 오후 8.30.29.png](Gradle%20032893aaf159450f856fd843fcb77420/한글.png)
    

<aside>
💡 compile deprecated.. (v7)

</aside>

- 다른 프로젝트의 의존관계 설정

```groovy
// settings.gradle
include 'subProject'

// build.gradle
dependencies {
	implementation project(':subProject')
}

// subProject가 없이 build할 경우
FAILURE: Build failed with an exception.

* Where:
Build file '/Users/woosanghun/IdeaProjects/Gradle_test2/build.gradle' line: 17

~~~~* What went wrong:
A problem occurred evaluating root project 'Gradle_test2'.
> Project with path ':subProject' could not be found in root project 'Gradle_test2'.
```

- 최신버전 정의

```groovy
dependencies {
	// 1 version에서 최신버전 설치
	implementation 'org.slf4j:slf4j-api:1.+'
	// 출시 버전 중 최신버전 설치
	implementation 'commons-cli:commons-cli:latest.integration'
	// 배포된 버전 중 최신버전 설치
	implementation 'junit:junit:latestrelease'
}
```

## 멀티 프로젝트 관리 (subprojects, allprojects)

[https://jojoldu.tistory.com/123?category=721560](https://jojoldu.tistory.com/123?category=721560)

- rootProject에서 moduleA, moduleB, moduleCommon프로젝트를 하위로 관리하겠다는 설정

```groovy
// settings.gradle
rootProject.name='rootProject'
include 'moduleA', 'moduleB', 'moduleCommon'
```

- build.gradle

```groovy
buildscript {
	...
}

// 루트 프로젝트 + 서브 프로젝트 
allprojects {

}

**subprojects {
	...
	// settings.gradle에 include된 프로젝트에관한 설정
	apply plugin: 'java'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  sourceCompatibility = 1.8

  repositories {
      mavenCentral()
  }

  dependencies {
      testCompile group: 'junit', name: 'junit', version: '4.12'
  }
}

// 하위 프로젝트간 의존성 관리
project(':moduleA') {
	dependencies {
		implementation project(':moduleCommon')
	}
}

project(':moduleB') {
	dependencies {
		implementation project(':moduleCommon')
	}
}**
```

- moduleA와 moduleB는 모두 moduleCommon에 의존하고 있기 때문에 설정

## 빌드 명령어 & 옵션

```bash
# 일반 빌드
gradle build

# 테스트 제외하고 빌드
gradle build --exclude-task test

# 빌드 과정을 확인하고 싶을 때
gradle build --console=verbose

# 다운로드한 라이브러리 캐쉬를 제거후 다시 빌드한다.
gradle build --refresh-dependencies

# war로 빌드
gradle war

# jar로 빌드
gradle jar
```

- war로 빌드할 시에 필요한 plugin 추가 (둘중 하나만 추가하면 됨)

```groovy
plugins {
	**id 'war'**
}

**apply plugin:'war'**
```

## Gradle 기본 실습

<aside>
💡 Intelij
jdk 8
spring boot 2.6.4

</aside>

### (1) Spring project 생성 (+ Gradle)

- gradle 버전에 맞는 jdk 선택
    - gradle(6.7) ⇒ jdk 1.8
- 프로젝트 구조
    
    ![스크린샷 2022-03-03 오전 10.36.29.png](Gradle%20032893aaf159450f856fd843fcb77420/한글_특수문자.png)
    
    - **gradle** : wrapper 관련 파일들
    - **src** : maven 구조와 동일, 소스코드 및 resource
    - **build.gradle** : gradle build script 설정파일
    - **gradlew(.bat):** gradle wrapper 실행파일 (mac, linux or window)
    - **settings.gradle** : gradle 설정파일
    

### (2) build.gradle 테스트

- dependencies에 라이브러리를 추가해보고 실제로 라이브러리가 추가되는지 확인한다.

```bash
plugins {
    id 'org.springframework.boot' version '2.6.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'war'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    **implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.4'
    implementation 'org.projectlombok:lombok:1.18.22'**
}

tasks.named('test') {
    useJUnitPlatform()
}
```

- spring boot starter data jpa와 lombok 라이브러리를 추가한다.
- External Libraries에 라이브러리가 추가되었는지 확인한다.

### (3) 외부 라이브러리가 추가되지 않았을 경우

- Intelij에서 가끔 refresh OR rebuild project를 진행해도 라이브러리가 추가 되지 않았을 때 수동으로 동기화 시키는 방법이 존재한다.
- View → Tool Windows → Gradle → Reload Gradle Project 클릭
    
    ![스크린샷 2022-03-03 오전 10.58.13.png](Gradle%20032893aaf159450f856fd843fcb77420/한글_숫자_2022-12.png)
    

### (4) War, Jar 빌드 테스트

```bash
**1. war 빌드
gradle war

2. jar 빌드
gradle jar

3. gradlew 사용 war 빌드 (배포용 (외장was))
./gradlew war

4. gradlew 사용 war 빌드 (단순 실행용 (내장was사용))
./gradlew bootWar**

5. lib 비우고 다시 빌드
./gradlew clean bootWar

6. 특정 task 무시
./gradlew bootWar --exclude-task test

7. 자세한정보 확인
./gradlew bootWar --info
```

- war는 외장 was서버에 배포할 목적으로 실행되는 task이며, bootWar는 내장톰캣이 함께 만들어져 단독실행이 가능하게 만들어주는 task이다.

- 성공하면 build/libs 아래에 해당 패키징파일 생성
    
    ![스크린샷 2022-03-03 오후 1.17.22.png](Gradle%20032893aaf159450f856fd843fcb77420/%E1%84%89%E1%85%B3%E1%84%8F%E1%85%B3%E1%84%85%E1%85%B5%E1%86%AB%E1%84%89%E1%85%A3%E1%86%BA_2022-03-03_%E1%84%8B%E1%85%A9%E1%84%92%E1%85%AE_1.17.22.png)
    

- war or jar파일이 존재하는 build 디렉터리가 생성되지 않을 경우
    - Project Structure → Excluded에 build나 out 디렉터리가 추가되어 있는지 확인 후 제거
    
    ![스크린샷 2022-03-03 오후 1.14.18.png](Gradle%20032893aaf159450f856fd843fcb77420/%E1%84%89%E1%85%B3%E1%84%8F%E1%85%B3%E1%84%85%E1%85%B5%E1%86%AB%E1%84%89%E1%85%A3%E1%86%BA_2022-03-03_%E1%84%8B%E1%85%A9%E1%84%92%E1%85%AE_1.14.18.png)
    

## 멀티 프로젝트 실습

### (1) root Project 생성

- File → New Project → Gradle → Additinal Libraries에 Java 선택

### (2) sub Project 생성

- 필요한만큼 sub project 생성
- New → Module → Gradle or Spring Initializr 선택 후 모듈 생성
- 반드시 root proejct의 **module**로 생성되어야 함
- 이클립스의 경우 sub 프로젝트의 경로가  루트 프로젝트의 Location 아래에 지정되어야 함
    - ex) .../Users/sanghun/git/**gradle-root**/**gradle-admin**

### (3) 프로젝트 구조 및 정리

![스크린샷 2022-03-07 오후 2.39.07.png](Gradle%20032893aaf159450f856fd843fcb77420/%E1%84%89%E1%85%B3%E1%84%8F%E1%85%B3%E1%84%85%E1%85%B5%E1%86%AB%E1%84%89%E1%85%A3%E1%86%BA_2022-03-07_%E1%84%8B%E1%85%A9%E1%84%92%E1%85%AE_2.39.07.png)

- 위와같이 gradle-root라는 루트 프로젝트안에 모듈로 3개의 서브 프로젝트가 들어감
- 프로젝트 루트에 src폴더는 삭제(사용하지 않음)
- 루트프로젝트에서 모든 설정과 빌드를 관리하므로 서브프로젝트의 gradle 폴더 및 gradlew관련 파일, settings.gradle파일은 필요가 없으며 src폴더(필요한경우)와 build.gradle만 존재하면 된다.
    - 애초에 프로젝트 생성 시 Gradle로 생성하면 귀찮음을 덜 수 있다..

### (5) root ↔ sub 관계 지정

- Gradle 프로젝트로 생성한 모듈은 자동으로 root 프로젝트에 include 되어 있지만, Spring Initializr로 생성한 모듈은 수동으로 등록해주어야 한다.
- settings.gradle에서 프로젝트를 포함시킬 수 있다.

```groovy
// settings.gradle (root)
rootProject.name = 'gradle-root'
include 'gradle-common'
include 'gradle-service'
include 'gradle-service-default'
include 'gradle-graphql'
// 수동 등록
**include 'gradle-webserver-admin'**
```

- multi project 관계 확인

```groovy
**./gradlew -q project**

------------------------------------------------------------
Root project 'gradle-root'
------------------------------------------------------------

Root project 'gradle-root'
+--- Project ':gradle-common'
+--- Project ':gradle-graphql'
+--- Project ':gradle-service'
+--- Project ':gradle-service-default'
\--- Project ':gradle-webserver-admin'
```

### (6) root Project build.gradle 수정

```groovy
buildscript {
    ext {
        springBootVersion= '2.6.4'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
    }
}

allprojects {
    group 'com.gradle'
    version '1.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    // 서브 프로젝트 공통 의존성 정의
    // spring dependency-management로 자동 버전설정
    dependencies {
        implementation 'org.projectlombok:lombok'
    }
}

project(':gradle-common') {
    dependencies {

    }
}

project(':gradle-service-default') {
    dependencies {
        implementation project(':gradle-service')
        implementation project(':gradle-common')
    }
}

project(':gradle-graphql') {
    dependencies {
        implementation project(':gradle-common')
    }
}

project(':gradle-webserver-admin') {
    dependencies {
        implementation project(':gradle-common')
        implementation project(':gradle-service-default')
        implementation project(':gradle-graphql')
    }
}
```

### (7) sub project build.gradle

```groovy
// main class 존재하지 않는 sub project
bootJar {
    enabled = false
}

jar {
    enabled = true
}

// main class있는 project
dependencies {
	// root project로 옮김
}
```

### (8) 멀티프로젝트 의존성 확인

- 위에서 sub project들 간에 서로 의존 관계를 지정한것을 직접 확인하고 싶으면 Gradle탭을 활용한다.
    
    ![스크린샷 2022-03-10 오전 11.34.34.png](Gradle%20032893aaf159450f856fd843fcb77420/%E1%84%89%E1%85%B3%E1%84%8F%E1%85%B3%E1%84%85%E1%85%B5%E1%86%AB%E1%84%89%E1%85%A3%E1%86%BA_2022-03-10_%E1%84%8B%E1%85%A9%E1%84%8C%E1%85%A5%E1%86%AB_11.34.34.png)
    
- 위와같이 dependency아래에 관계를 맺은 프로젝트(모듈)들이 존재하면 성공

### (9) Build 테스트

```groovy
// build
./gradlew build

// 특정모듈 빌드
./gradlew clean build -p **module-name**
```

<aside>
💡 error: package org.junit.jupiter.api does not exist...

</aside>

- Gradle build 시 Test관련 해서 계속 에러가 난다면?
    - spring-boot-starter-test 추가 확인
    - root project → spring-boot 버전 확인

### (10) 동적 스크립트

- properties를 활용해 프로그래밍방식으로 동적으로 스크립트를 실행시킬 수 있다.

### (11) 실제 의존 테스트..

- common → dto
- service → interface
- service-default → impl (dto + interface)

## Nexus 연동

### (1) Nexus 레포지토리 사용하기

```groovy
repositories {
    maven {
        credentials {
            username=nexusUsername
            password=nexusPassword
        }
        url nexusUrl+"/bandi-maven-repository"
        **allowInsecureProtocol=true**
    }
}

dependencies {
    implementation 'com.bandisnc:bandisnc-common-service-default:1.2.6'
}

```

- 최신 그래들에서는 보안을 위해 http서버를 사용하는 것을 허용하지 않고 있는데 이것을 강제로 사용하게하는 옵션이 allowInsecureProtocol이다.

### (2) Nexus 배포

```groovy
subprojects {
	apply plugin: 'maven-publish'

	// Gradle 6만 가능
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: nexusUrl+'/bandi-maven-repository') {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
                snapshotRepository(url: '/bandi-maven-snapshot') {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
            }
        }
    }
}

```

- 7버전은 다른방식으로 해야됨,.. 테스트중 에러

- 최종코드

```groovy
buildscript {
    ext {
        springBootVersion='2.6.4'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

allprojects {
    // root + subproject 설정
    group 'com.example.package'
    version '1.0.0-SNAPSHOT'
}

subprojects {
    // subproject 설정
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility=1.8

    repositories {
        // mavenCentral()
        maven {
            credentials {
                username=nexusUsername
                password=nexusPassword
            }
            url nexusUrl+'/bandi-maven-repository'
            allowInsecureProtocol=true
        }
    }

    // 서브프로젝트 공통 의존성 정의
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        testImplementation 'junit:junit'
    }

}

project(':gradle-common') {
    dependencies {

    }
}

project('gradle-service') {
    dependencies {
        implementation project(':gradle-common')
    }
}

project('gradle-service-default') {
    dependencies {
        implementation project(':gradle-service')
        implementation project(':gradle-common')
        implementation 'com.bandisnc:bandisnc-license-service-default:1.2.6'
    }
}

if (profile == 'A') {
    project('gradle-webserver-admin') {
        apply plugin:'war'
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-web'
            providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            implementation project(':gradle-service-default')
        }
    }
} else {
    println('profile other')
    project('gradle-webserver-admin') {
        apply plugin:'war'
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-web'
            providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            implementation project(':gradle-service-default')
            implementation project(':gradle-common')
        }
    }
}
```
